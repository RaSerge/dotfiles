" Modeline and Notes {{{
" vim: set foldlevel=0 foldmethod=marker spell formatoptions=jcrql:
" }}}

" Initialization {{{
    " Define a global autogroup used through the entire vimrc
    augroup vimrc
        autocmd!
    augroup end
" }}}

" Vim-plug {{{
    " Auto-install Vim-plug (if needed)
    if empty(glob('~/.vim/autoload/plug.vim')) && executable('curl')
        silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
                    \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
        autocmd vimrc VimEnter * PlugInstall
    endif
    call plug#begin('~/.vim/plugged')

    " The list of installed plugins
    Plug 'Konfekt/FastFold'             " Better fold refresh
    Plug 'bogado/file-line'             " Open vim file:line
    Plug 'christoomey/vim-sort-motion'  " Add gl sort motion
    Plug 'cohama/lexima.vim'            " Wonderful auto-pair
    Plug 'ctrlpvim/ctrlp.vim'           " Fuzzy file, buffer, MRU, tag finder
    Plug 'honza/vim-snippets'           " Vim snippets
    Plug 'junegunn/vim-peekaboo'        " See content of registers
    Plug 'justinmk/vim-sneak'           " Sneak motion
    Plug 'ludovicchabant/vim-gutentags' " Automatic tag management
    Plug 'scrooloose/syntastic'         " Syntax Checkers
    Plug 'tommcdo/vim-lion'             " Add gl/gL to align text
    Plug 'tpope/vim-commentary'         " Toggle comments
    Plug 'tpope/vim-repeat'             " Enable supported plugin actions to be repeated with '.'
    Plug 'tpope/vim-surround'           " All about surrounding
    Plug 'tpope/vim-vinegar'            " Lightweight file manager
    Plug 'vim-airline/vim-airline'      " Better statusbar
    Plug 'wellle/targets.vim'           " Additional text objects

    " Lazy loaded plugins / platform specific plugins
    Plug 'Valloric/YouCompleteMe',
                \ { 'do': './install.py --clang-completer --gocode-completer',
                \ 'on': [] } " Completion engine
    Plug 'SirVer/ultisnips',
                \ { 'on': [] } " Snippets engine

    Plug 'Shougo/deoplete.nvim',
                \ has('nvim') ? {'do': ':UpdateRemotePlugins'}
                \ : { 'on': [] } " Completion engine - only works with NeoVim

    " Appearance
    Plug 'junegunn/rainbow_parentheses.vim' " Rainbow parentheses
    Plug 'fatih/molokai'                    " Molokai color scheme

    " Language specific plugins {{{
        " Python
        Plug 'klen/python-mode', { 'for': 'python' } " VIM as a powerful Python IDE

        " Markdown
        Plug 'jtratner/vim-flavored-markdown',
                    \ { 'for' : 'markdown' }         " GitHub flavored markdown
        Plug 'suan/vim-instant-markdown',
                    \ { 'for' : 'markdown' }         " Instant markdown preview in browser (requirements on GitHub)

        " Latex
        Plug 'lervag/vimtex', { 'for': 'tex' }

        " Go
        Plug 'fatih/vim-go', { 'for': 'go' }
    " }}}

    " Deoplete specific plugins {{{
        Plug 'zchee/deoplete-jedi',
                    \ has('nvim') ? {  } : { 'on': [] }              " Python completion
        Plug 'zchee/deoplete-go',
                    \ has('nvim') ? { 'do': 'make' } : { 'on': [] }  " Go completion
        Plug 'zchee/deoplete-clang',
                    \ has('nvim') ? {  } : { 'on': [] }              " Clang completion
    " }}}

    " Enable Vim-stay after all the other plugins
    Plug 'kopischke/vim-stay'                 " Save and restore cursor position, folds and so on

    " ...Vim-plug is done
    call plug#end()
" }}}

" General {{{
    " Sets how many lines of history VIM has to remember
    set history=10000 " Maximum value for history

    " Modeline in the first five lines
    set modeline
    set modelines=3

    " Mouse
    set mouse=a
    set mousehide               " Hide the mouse cursor while typing

    " End of line (unix EOL is preferred over the dos one and before the mac one).
    set fileformats=unix,dos,mac

    " With a map leader it's possible to do extra key combinations
    let g:mapleader = "\<Space>"
    let g:maplocalleader = "\<Space>"
" }}}

" Backup, swap, info and ignore files {{{
    set noswapfile  " Stop annoying swap files

    " Save your backups to a less annoying place than the current directory.
    " If you have .vim-backup in the current directory, it'll use that.
    " Otherwise it saves it to ~/.vim/backup or . if all else fails.
    if isdirectory($HOME . '/.vim/backup') == 0
        :silent !mkdir -p ~/.vim/backup >/dev/null 2>&1
    endif
    set backupdir-=.
    set backupdir+=.
    set backupdir-=~/
    set backupdir^=~/.vim/backup/
    set backupdir^=./.vim-backup/
    set backup

    " Save your swap files to a less annoying place than the current directory.
    " If you have .vim-swap in the current directory, it'll use that.
    " Otherwise it saves it to ~/.vim/swap, ~/tmp or .
    if isdirectory($HOME . '/.vim/swap') == 0
        :silent !mkdir -p ~/.vim/swap >/dev/null 2>&1
    endif
    set directory=./.vim-swap//
    set directory+=~/.vim/swap//
    set directory+=~/tmp//
    set directory+=.

    " Tell (n)vim to remember certain things when we exit
    "  '20  :  marks will be remembered for up to 10 previously edited files
    "  "100 :  will save up to 100 lines for each register
    "  :1000  :  up to 1000 lines of command-line history will be remembered
    "  %    :  saves and restores the buffer list
    "  n... :  where to save the viminfo files
    if has('nvim')
        set shada='20,<100,:1000,%,n~/.nvim/nviminfo
        set viewdir=~/.vim/view
    else
        set viminfo='20,\"100,:100,%,n~/.vim/viminfo
    endif

    if exists('+undofile')
        " undofile - This allows you to use undos after exiting and restarting
        " This, like swap and backups, uses .vim-undo first, then ~/.vim/undo
        " :help undo-persistence
        " This is only present in 7.3+
        if isdirectory($HOME . '/.vim/undo') == 0
            :silent !mkdir -p ~/.vim/undo > /dev/null 2>&1
        endif
        set undodir=./.vim-undo//
        set undodir+=~/.vim/undo//
        set undofile
    endif

    " Ignore unreadable files, images, etc.
    set wildignore=*.o,*~,*.pyc
    set wildignore+=*.png,*.gif,*.jpg,*.ico
    set wildignore+=.git,.svn,.hg
    set wildignore+=*/tmp/*,*.so,*.swp,*.zip

    " Save cursor position, folds, and so on.
    set viewoptions=cursor,folds,slash,unix
" }}}

" Appearance {{{
    "Always show current position
    set cursorline                   " highlights the current line

    " Height of the command bar
    set cmdheight=1

    set background=dark
    colorscheme molokai

    set scrolloff=8       " don't scroll any closer to top/bottom
    set sidescrolloff=5   " don't scroll any closer to left/right

    " No annoying sound on errors
    set noerrorbells visualbell t_vb=
    autocmd vimrc GUIEnter * set visualbell t_vb=

    " Line and relative numbers
    set number
    set relativenumber

    set showmatch                   " Show matching brackets/parenthesis
    set matchtime=2                 " How many tenths of seconds to blink matching bracket/parenthesis

    set linespace=0                 " No extra spaces between rows in GUI

    " Allow the cursor to be one more the last char.
    set virtualedit=onemore

    " Disable split separator characters
    set fillchars=""

    " Show snippet_complete marker (if any)
    if has('conceal')
        set conceallevel=2 concealcursor=i
    endif
" }}}

" Auto completion {{{
    " How to show completion informations in insert mode
    set completeopt=menuone,longest

    " Clever tab to cycle the completion popup menu
    " If you even need to insert a literal tab, press <CTRL-V><Tab>
    function! CleverTab()
        if strpart(getline('.'), 0, col('.') -1) =~# '^\s*$'
            return "\<Tab>"
        else
            return "\<C-N>"
        endif
    endfunction
    inoremap <Tab> <C-R>=CleverTab()<CR>

    if has('patch-7.4.314')
        set shortmess+=c " Quiet completions
    endif
" }}}

" Search and commands {{{
    set showcmd               " Display incomplete commands

    " Magic pattern matching
    set magic

    set incsearch             " Find as you type search
    set hlsearch              " Highlight search terms
    set ignorecase            " Case insensitive search
    set smartcase             " Case sensitive when upper case is present
    set gdefault

    set wildmenu              " Show list instead of just completing
    set wildmode=longest,full " Command <Tab> completion, list matches, then longest common part, then all.

    " Use sane magic regexes
    nnoremap / /\v
    vnoremap / /\v
" }}}

" Text, lines, tab, indent and folding {{{
    " Set utf-8 as standard encoding
    if !has('nvim') " NeoVim defaults to this
        set encoding=utf-8                             " The encoding displayed.
    end
    scriptencoding utf-8                               " Specify the encoding of this file
    set fileencoding=utf-8                             " The encoding written to file.

    set backspace=indent,eol,start                     " Backspace for dummies
    set whichwrap=b,s,h,l,<,>,[,]                      " Backspace and cursor keys wrap too

    " White spaces
    set list                                           " Display white spaces
    set listchars=tab:→\ ,trail:•,                     " Custom characters highlights
    set listchars+=extends:⟩,precedes:⟨,
    set listchars+=\nbsp:␣,conceal:*,
    let &showbreak = '↪'                               " Show whether lines have been wrapped

    " Tabs
    set expandtab                                      " In insert mode, insert the appropriate number of spaces to insert a Tab
    set smarttab                                       " Insert tabs in front of lines according to shiftwidth
    set shiftwidth=4                                   " Operation >> indents 4 columns; << un-indents 4 columns
    set tabstop=4                                      " A hard TAB displays as 4 columns
    set expandtab                                      " Insert spaces when hitting TABs
    set softtabstop=4                                  " Insert/delete 4 spaces when hitting a TAB/BACKSPACE
    set shiftround                                     " Round indent to multiple of 'shiftwidth'
    set autoindent                                     " Align the new line indent with the previous line
    set cindent                                        " Automatic C programming indenting

    " Line wrapping
    set wrap                                           " Visually wrap lines too long
    set textwidth=0 wrapmargin=0                       " Turn off physical line wrapping

    " Do not insert comment leader on 'o'
    " Needs an autocmd because different filetypes override this option
    autocmd vimrc FileType * setlocal formatoptions-=o

    " Filetype specific settings {{{
        " Git commit
        autocmd vimrc Filetype gitcommit setlocal spell textwidth=72

        " Latex
        autocmd vimrc Filetype tex setlocal spell conceallevel=0 synmaxcol=200 ttyfast lazyredraw

        " Markdown
        autocmd vimrc Filetype ghmarkdown,markdown,md setlocal spell
    " }}}
" }}}

" Tabs, windows and buffers {{{
    set hidden  " Allows to change buffer without saving

    " Open new split panes to right and bottom, which feels more natural
    set splitbelow splitright

    " Set to auto read when a file is changed from the outside
    set autoread

    " Switch CWD to the directory of the open buffer
    set autochdir

    " Auto save on :next, :edit, :quit, etc.
    set autowriteall

    " ... and on focus lost
    autocmd vimrc FocusLost * :silent! wall

    " Smart way to move between windows
    nnoremap <C-h> <C-w>h
    nnoremap <C-j> <C-w>j
    nnoremap <C-k> <C-w>k
    nnoremap <C-l> <C-w>l

    " Same smart ways with nvim terminal buffers
    if has('nvim')
        tnoremap <Esc> <C-\><C-n>
        tnoremap <C-h> <C-\><C-n><C-w>h
        tnoremap <C-j> <C-\><C-n><C-w>j
        tnoremap <C-k> <C-\><C-n><C-w>k
        tnoremap <C-l> <C-\><C-n><C-w>l
    endif

    " Close the current buffer
    nnoremap <leader>bd :bd<cr>

    " Close all the buffers
    nnoremap <leader>ba :bufdo bdelete<cr>

    " Move to next buffer
    nnoremap <leader>bn :bnext<cr>
    nnoremap <leader>bp :bprevious<cr>

    " Useful mappings for managing tabs
    nnoremap <leader>tn :tabnew<cr>
    nnoremap <leader>to :tabonly<cr>
    nnoremap <leader>tc :tabclose<cr>
    nnoremap <leader>tm :tabmove

    " Specify the behavior when switching between buffers
    try
        set switchbuf=useopen,usetab,newtab
        set showtabline=2
    catch
    endtry

    " Code folding mappings
    nmap <leader>f0 :set foldlevel=0<CR>
    nmap <leader>f1 :set foldlevel=1<CR>
    nmap <leader>f2 :set foldlevel=2<CR>
    nmap <leader>f3 :set foldlevel=3<CR>

    " Exclude quickfix windows from buflist
    autocmd vimrc FileType qf set nobuflisted

    " Resize splits when the window is resized
    autocmd vimrc VimResized * :wincmd =
" }}}

" Misc mappings {{{
    " Remap VIM 0 to first non-blank character
    map 0 ^

    " Treat long lines as break lines (useful when moving around in them)
    noremap j gj
    noremap k gk
    noremap gj j
    noremap gk k

    " Disable highlight when <leader><cr> is pressed
    nnoremap <silent> <leader><cr> :noh<cr>

    " Toggle paste mode on and off
    nnoremap <leader>pp :setlocal paste!<cr>

    " Make Y yank everything from the cursor to the end of the line.
    noremap Y y$

    " Quickly call 'make'
    nnoremap <leader>m :make<cr>

    " loclist mappings
    nnoremap <leader>lo :lopen<cr>
    nnoremap <leader>ln :lnext<cr>
    nnoremap <leader>lp :lprev<cr>

    " Use 'c*' to change the word under the cursor, repeat with '.'
    nnoremap c* *<C-o>cgn

    if &term =~? '^screen'
        " tmux will send xterm-style keys when xterm-keys is on
        execute "set <xUp>=\e[1;*A"
        execute "set <xDown>=\e[1;*B"
        execute "set <xRight>=\e[1;*C"
        execute "set <xLeft>=\e[1;*D"
    endif
" }}}

" Spell checking {{{
    " Shortcuts
    nnoremap <leader>ss :setlocal spell!<cr>
    nnoremap <leader>sn ]s
    nnoremap <leader>sp [s
    nnoremap <leader>sa zg
    nnoremap <leader>s? z=
" }}}

" Misc {{{
    set iskeyword-=.                    " '.' is an end of word designator
    set iskeyword-=#                    " '#' is an end of word designator
    set iskeyword-=-                    " '-' is an end of word designator

    " Highlight TODO, FIXME, NOTE, etc.
    autocmd vimrc Syntax * call matchadd('Todo',  '\W\zs\(TODO\|FIXME\|CHANGED\|XXX\|BUG\|HACK\)')
    autocmd vimrc Syntax * call matchadd('Debug', '\W\zs\(NOTE\|INFO\|IDEA\|DEBUG\)')

    if executable('ag')
        set grepprg=ag\ --vimgrep\ $*
        set grepformat=%f:%l:%c:%m
    elseif executable('ack')
        set grepprg=ack\ -k
    endif

    if has('nvim')
        set termguicolors
    endif
" }}}

" Helper functions {{{
    " Strip whitespace {{{
    function! StripTrailingWhitespace()
        " Preparation: save cursor position
        " (last search is automatically saved because inside a function)
        let l:save = winsaveview()
        " Do the business.
        %s/\s\+$//e
        " Clean up: restore cursor position.
        call winrestview(l:save)
    endfunction
    " }}}

    " Before saving, delete trailing white-spaces and ^M
    autocmd vimrc FileType c,cpp,java,php,javascript,python,twig,xml,yaml,vim,tex,lua
                \ autocmd vimrc BufWritePre <buffer> call StripTrailingWhitespace()
" }}}

" Plugin settings {{{
    " Lazy plugin loading
    if !has('nvim')
        autocmd vimrc InsertEnter * call plug#load('YouCompleteMe')
    endif

    " CtrlP {{{
        let g:ctrlp_map = '<c-p>'
        let g:ctrlp_cmd = 'CtrlPMixed'

        " Use the nearest .git directory as the cwd
        " This makes a lot of sense if you are working on a project that is
        " in version control. It also supports works with .svn, .hg, .bzr.
        let g:ctrlp_working_path_mode = 'ra'

        let g:ctrlp_cache_dir = $HOME.'/.vim/.cache/ctrlp'
        let g:ctrlp_match_window_reversed = 1
        let g:ctrlp_clear_cache_on_exit = 0

        " Allow a maximum depth of 5 sub-folders and a total of 1000 files.
        let g:ctrlp_max_depth = 10
        let g:ctrlp_max_files = 10000

        " Exclude ZSH scratches
        let g:ctrlp_mruf_exclude = '/tmp/zsh.*\|/private/tmp/zsh.*'

        " Manually enable CtrlP extensions.
        let g:ctrlp_extensions = ['buffertag', 'quickfix', 'undo', 'line', 'changes', 'mixed']

        nnoremap <leader><space> :CtrlPBufTag<cr>
    " }}}

    " Vim Airline / Statusbar {{{
        let g:bufferline_echo = 0
        set noshowmode   " do not show default mode indicator
        set laststatus=2 " always show the statusbar

        " general configuration
        let g:airline#extensions#disable_rtp_load = 1 " do not find extensions in run time path
        let g:airline_powerline_fonts             = 1 " powerline fonts

        " custom symbols
        if !exists('g:airline_symbols')
            let g:airline_symbols = {}
        endif

        let g:airline_symbols.linenr    = '#'
        let g:airline_symbols.maxlinenr = ''
        let g:airline_symbols.spell     = 'S'
        let g:airline_symbols.paste     = 'P'

        " extensions
        " only load what you want
        let g:airline_extensions = [ 'syntastic', 'ctrlp', 'virtualenv', 'tabline' ]

        let g:airline#extensions#tabline#fnamemod   = ':t' " only show filename in tab
        let g:airline#extensions#quickfix#quickfix_text = 'Quickfix'
        let g:airline#extensions#quickfix#location_text = 'Location'

    " }}}

    " Syntastic {{{
        let g:syntastic_check_on_open            = 1 " Automatically check on file open
        let g:syntastic_always_populate_loc_list = 1 " Fill location list
        let g:syntastic_auto_loc_list            = 2 " Close it when no errors are detected

        let g:syntastic_error_symbol         = "\u2718"
        highlight SyntasticErrorSign ctermbg=235 ctermfg=161 guifg=#F92672 guibg=#232526

        let g:syntastic_warning_symbol       = "\u2718"
        highlight SyntasticWarningSign ctermbg=235 ctermfg=144 guifg=#E6DB74 guibg=#232526

        let g:syntastic_style_error_symbol   = "\u26A0"
        highlight link SyntasticStyleErrorSign SyntasticErrorSign

        let g:syntastic_style_warning_symbol   = "\u26A0"
        highlight link SyntasticStyleWarningSign SyntasticWarningSign

        " Aggregate checkers
        let g:syntastic_aggregate_errors = 1

        " Syntastic status line for airline
        let g:syntastic_stl_format = '%E{#%e Errs: %fe}%B{, }%W{#%w Warns: %fw}'

        " Language specific checkers {{{
            " Java
            let g:syntastic_java_checkers = []

            " Go
            let g:syntastic_go_checkers = ['golint', 'govet', 'errcheck']

            " Enable again perl checker
            let g:syntastic_enable_perl_checker = 1
            let g:syntastic_perl_checkers = ['perl']

            " Enable check C headers file
            let g:syntastic_c_check_header = 1
            let g:syntastic_c_compiler_options = '-Wall'

            " Enable C++11 standard
            let g:syntastic_cpp_compiler_options='-std=c++11 -stdlib=libc++ -Wall'

            " Javascript
            let g:syntastic_javascript_jshint_args = '--extract=auto'

            " Lua
            let g:syntastic_lua_checkers = ['luac', 'luacheck']

            " VimL
            " let g:syntastic_vim_checkers = ['vint']
        " }}}
    " }}}

    " Rainbow parentheses {{{
        " List of colors that you do not want. ANSI code or #RRGGBB
        let g:rainbow#blacklist = [225, 245, 241, 244, 193]

        let g:rainbow#pairs = [['(', ')'], ['[', ']'], ['{', '}']]
        let g:rainbow#max_level = 16

        autocmd vimrc BufReadPre,FileReadPre * :RainbowParentheses
    " }}}

    " YouCompleteMe {{{
        let g:ycm_autoclose_preview_window_after_completion = 1

        let g:ycm_collect_identifiers_from_tags_files       = 1 " enable completion from tags
        let g:ycm_use_ultisnips_completer                   = 1 " Default 1, just ensure
        let g:ycm_seed_identifiers_with_syntax              = 1 " Completion for programming language's keyword
        let g:ycm_complete_in_comments                      = 1 " Completion in comments
        let g:ycm_complete_in_strings                       = 1 " Completion in string
        let g:ycm_add_preview_to_completeopt                = 0 " Add preview to completeopt

        let g:ycm_show_diagnostics_ui                       = 0 " Disable YCM syntax checker in favour of syntastic
        let g:ycm_key_detailed_diagnostics                  = '' " Disable YCM detailed diagnostic

        nnoremap <leader>g :YcmCompleter GoToDefinitionElseDeclaration<CR>
    " }}}

    " Deoplete {{{
        let g:deoplete#enable_at_startup = 1 " Enable Deoplete
        let g:deoplete#auto_complete_delay = 20 " ms delay to trigger autocomplete
        let g:deoplete#auto_refresh_delay = 20 " ms delay to refresh autocomplete

        let g:deoplete#sources = {}
        let g:deoplete#sources._ = ['buffer', 'member', 'tag', 'file', 'omni', 'ultisnips']
        let g:deoplete#sources.tex = ['buffer', 'member', 'dictionary', 'file', 'omni', 'ultisnips']
        let g:deoplete#sources.python = ['buffer', 'member', 'jedi', 'file', 'omni', 'ultisnips']
        let g:deoplete#sources.go = ['buffer', 'member', 'go', 'file', 'omni', 'ultisnips']
        let g:deoplete#sources.c = ['buffer', 'member', 'clang', 'file', 'omni', 'ultisnips']
        let g:deoplete#sources.cpp = ['buffer', 'member', 'clang', 'file', 'omni', 'ultisnips']
        let g:deoplete#sources.objc = ['buffer', 'member', 'clang', 'file', 'omni', 'ultisnips']

        let g:deoplete#max_list = 20 " Show 10 entries at most
        let g:deoplete#max_menu_width = 20 " Matches the list length

        if has('nvim')
            call deoplete#custom#set('ultisnips', 'rank', 1000) " Keep snippets on top
        endif

        " Go completion settings
        let g:deoplete#sources#go#gocode_binary = $GOPATH.'/bin/gocode'
        let g:deoplete#sources#go#sort_class = ['package', 'func', 'type', 'var', 'const']
        let g:deoplete#sources#go#use_cache = 0

        " vim-tex integration
        if !exists('g:deoplete#omni#input_patterns')
            let g:deoplete#omni#input_patterns = {}
        endif
        let g:deoplete#omni#input_patterns.tex = '\\(?:'
                    \ .  '\w*cite\w*(?:\s*\[[^]]*\]){0,2}\s*{[^}]*'
                    \ . '|\w*ref(?:\s*\{[^}]*|range\s*\{[^,}]*(?:}{)?)'
                    \ . '|hyperref\s*\[[^]]*'
                    \ . '|includegraphics\*?(?:\s*\[[^]]*\]){0,2}\s*\{[^}]*'
                    \ . '|(?:include(?:only)?|input)\s*\{[^}]*'
                    \ . '|\w*(gls|Gls|GLS)(pl)?\w*(\s*\[[^]]*\]){0,2}\s*\{[^}]*'
                    \ . '|includepdf(\s*\[[^]]*\])?\s*\{[^}]*'
                    \ . '|includestandalone(\s*\[[^]]*\])?\s*\{[^}]*'
                    \ .')'
    " }}}

    " UltiSnips {{{
        " Remap UltiSnips for compatibility with completion handlers
        let g:UltiSnipsExpandTrigger                        = '<C-j>'
        let g:UltiSnipsJumpForwardTrigger                   = '<C-j>'
        let g:UltiSnipsJumpBackwardTrigger                  = '<C-k>'

        " This function only runs when UltiSnips is not loaded (credits github/saaguero)
        function! LoadUltiSnips()
            let l:curpos = getcurpos()
            execute plug#load('ultisnips')
            call cursor(l:curpos[1], l:curpos[2])
            call UltiSnips#ExpandSnippet()
            " Add command to manually list snippets
            command UltiSnipsListSnippets call UltiSnips#ListSnippets()
            return ''
        endfunction
        inoremap <silent> <C-j> <C-r>=LoadUltiSnips()<cr>

        " Give higher priority to UltiSnips specific snippets over SnipMate ones
        let g:UltiSnipsSnippetDirectories=[$HOME . '/.vim/plugged/vim-snippets/UltiSnips']
        let g:UltiSnipsEnableSnipMate=1

        " Snippets variables
        let g:author='Adriano Di Luzio'
        let g:snips_author=g:author
        let g:email='adrianodl@hotmail.it'
        let g:snips_email=g:email
        let g:github='https://github.com/aldur'
        let g:snips_github=g:github
    " }}}

    " GitHub Flavored Markdown {{{
        " Set GitHub flavour for all markdown files
        autocmd vimrc BufNewFile,BufRead *.md,*.markdown setlocal filetype=ghmarkdown
    " }}}

    " Vim Instant Markdown {{{
        let g:instant_markdown_slow = 1
    " }}}

    " Py-Mode {{{
        let g:pymode = 1
        let g:pymode_warnings = 0

        let g:pymode_trim_whitespaces = 0  " We already do that
        let g:pymode_options = 0
        let g:pymode_options_colorcolumn = 0
        let g:pymode_indent = 1
        let g:pymode_folding = 0
        let g:pymode_motion = 1

        let g:pymode_doc = 1
        let g:pymode_doc_bind = 'K'

        let g:pymode_breakpoint = 0
        let g:pymode_virtualenv = 1

        " Enable manual linting only
        let g:pymode_lint = 1
        let g:pymode_lint_on_write = 0
        let g:pymode_lint_unmodified = 0
        let g:pymode_lint_on_fly = 0
        let g:pymode_lint_message = 0

        let g:pymode_syntax = 1
        let g:pymode_syntax_all = 1
        let g:pymode_syntax_print_as_function = 1
        let g:pymode_syntax_slow_sync = 1

        let g:pymode_rope = 0
        let g:pymode_rope_completion = 0
        let g:pymode_rope_complete_on_dot = 0
        let g:pymode_rope_lookup_project = 0
        let g:pymode_rope_autoimport = 0

        let g:pymode_quickfix_minheight = 3
        let g:pymode_quickfix_maxheight = 3

        " Automatically fix PEP8 errors in the current buffer:
        autocmd vimrc Filetype python noremap <F8> :PymodeLintAuto<CR>
    "}}}

    " Vimtex settings {{{
        let g:vimtex_enabled = 1
        let g:tex_flavor = 'latex'  " Open '.tex' files as 'tex' and not 'plaintex'.

        let g:vimtex_quickfix_mode = 2  " Auto-open with no focus.
        let g:vimtex_latexmk_build_dir = 'build'

        if has('mac')
            " Skim as a PDF viewer
            let g:vimtex_view_general_viewer = '/Applications/Skim.app/Contents/SharedSupport/displayline'
            let g:vimtex_view_general_options = '-r @line @pdf @tex'

            " This adds a callback hook that updates Skim after compilation
            let g:vimtex_latexmk_callback_hooks = ['UpdateSkim']
            function! UpdateSkim(status)
                if !a:status | return | endif

                let l:out = b:vimtex.out()
                let l:cmd = [g:vimtex_view_general_viewer, '-r']
                if !empty(system('pgrep Skim'))
                    call extend(l:cmd, ['-g'])
                endif
                if has('nvim')
                    call jobstart(l:cmd + [line('.'), l:out])
                elseif has('job')
                    call job_start(l:cmd + [line('.'), l:out])
                else
                    call system(join(l:cmd + [line('.'), shellescape(l:out)], ' '))
                endif
            endfunction
        end

        if !has('nvim')
            " YCM semantic completion
            if !exists('g:ycm_semantic_triggers')
                let g:ycm_semantic_triggers = {}
            endif
            let g:ycm_semantic_triggers.tex = [
                        \ 're!\\[A-Za-z]*cite[A-Za-z]*(\[[^]]*\]){0,2}{[^}]*',
                        \ 're!\\[A-Za-z]*ref({[^}]*|range{([^,{}]*(}{)?))',
                        \ 're!\\hyperref\[[^]]*',
                        \ 're!\\includegraphics\*?(\[[^]]*\]){0,2}{[^}]*',
                        \ 're!\\(include(only)?|input){[^}]*',
                        \ 're!\\\a*(gls|Gls|GLS)(pl)?\a*(\s*\[[^]]*\]){0,2}\s*\{[^}]*',
                        \ 're!\\includepdf(\s*\[[^]]*\])?\s*\{[^}]*',
                        \ 're!\\includestandalone(\s*\[[^]]*\])?\s*\{[^}]*',
                        \ ]
        endif

        " Disable insert mode mappings
        let g:vimtex_imaps_enabled = 0

        " Integration with vim-surround
        autocmd vimrc FileType tex call s:latexSurround()

        " Better formatting with gq
        let g:vimtex_format_enabled = 1

        function! s:latexSurround()
            let b:surround_{char2nr("e")}
                        \ = "\\begin{\1environment: \1}\n\t\r\n\\end{\1\1}"
            let b:surround_{char2nr("c")} = "\\\1command: \1{\r}"
        endfunction
    " }}}

    " Vim-easy-align {{{
        " Start interactive EasyAlign in visual mode (e.g. vip<Enter>)
        vmap <Enter> <Plug>(EasyAlign)

        " Start interactive EasyAlign for a motion/text object (e.g. gaip)
        nmap ga <Plug>(EasyAlign)
    " }}}

    " Molokai {{{
        let g:molokai_original = 0 " Prefer gray background color
        let g:rehash256 = 1        " Enable experimental 256 colors support
    " }}}

    " Vim-Go {{{
        " Enable better syntax highlighting
        let g:go_highlight_functions = 1
        let g:go_highlight_methods = 1
        let g:go_highlight_fields = 1
        let g:go_highlight_structs = 1
        let g:go_highlight_interfaces = 1
        let g:go_highlight_operators = 1
        let g:go_highlight_build_constraints = 1

        " On recent VIM version better go fmt should work
        let g:go_fmt_experimental = 1

        " Show info for word under cursor
        let g:go_auto_type_info = 1

        " Mappings
        autocmd vimrc FileType go nmap <leader>r <Plug>(go-run)
        autocmd vimrc FileType go nmap <leader>b <Plug>(go-build)
        autocmd vimrc FileType go nmap <leader>t <Plug>(go-test)
        autocmd vimrc FileType go nmap <leader>c <Plug>(go-coverage)

        autocmd vimrc FileType go nmap <Leader>ds <Plug>(go-def-split)
        autocmd vimrc FileType go nmap <Leader>dv <Plug>(go-def-vertical)
        autocmd vimrc FileType go nmap <Leader>dt <Plug>(go-def-tab)

        autocmd vimrc FileType go nmap <Leader>gd <Plug>(go-doc)
        autocmd vimrc FileType go nmap <Leader>gv <Plug>(go-doc-vertical)

        autocmd vimrc FileType go nmap <Leader>e <Plug>(go-rename)
        autocmd vimrc FileType go nmap <Leader>i <Plug>(go-imports)
        autocmd vimrc FileType go nmap <Leader>s <Plug>(go-implements)

        " Syntastic integration
        let g:go_list_type = 'quickfix'
    " }}}

    " vim-gutentags {{{
        if isdirectory($HOME . '/.vim/tags/') == 0
            :silent !mkdir -p ~/.vim/tags >/dev/null 2>&1
        endif
        let g:gutentags_cache_dir = $HOME . '/.vim/tags/'
    " }}}
" }}}

" Ending settings {{{
    if filereadable(expand('~/.vimrc.local'))
        source ~/.vimrc.local
    endif

    set secure
" }}}
